/* 
 * The Maven coordinates for the project artifact
 */
ext.title = 'tutorial7'
description = 'This is tutorial7 example'
group = 'com.example'
version = '1.0.0'

/* 
 * The Gradle plugins 
 */
apply plugin: 'maven-publish'

/* 
 * The Gradle task dependencies 
 */
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
        classpath 'io.opencaesar.owl:owl-fuseki-gradle:3.+'
        classpath 'io.opencaesar.owl:owl-query-gradle:3.+'
        classpath 'io.opencaesar.owl:owl-load-gradle:3.+'
        classpath 'io.opencaesar.owl:owl-reason-gradle:3.+'
        classpath 'io.opencaesar.oml:oml-merge-gradle:2.+'
        classpath 'io.opencaesar.adapters:oml2owl-gradle:2.+'
	}
}

/*
 * Dataset-specific variables
 */
ext.dataset = [
    // Name of dataset (matches one used in .fuseki.ttl file)
    name: 'tutorial',
    // Root ontology IRI of the vocabulary bundle.
    rootVocabularyBundleIri: 'http://example.com/tutorial/vocabulary/bundle',
    // Root ontology IRI of the description bundle.
    rootDescriptionBundleIri: 'http://example.com/tutorial/description/bundle',

]

/*
 * The repositories to look up OML dependencies in
 */
repositories {
    mavenLocal()
    mavenCentral()
}

/*
 * The configuration of OML dependencies
 */
configurations {
    oml
}

/*
 * The OML dependencies
 */
dependencies {
    //oml "io.opencaesar.ontologies:core-vocabularies:$coreVersion"
    oml "io.opencaesar.ontologies:metrology-vocabularies:7.+"
}

/*
 * A task to extract and merge the OML dependencies
 */
task omlDependencies(type:io.opencaesar.oml.merge.OmlMergeTask, group:"oml") {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwlVEntailments(type:io.opencaesar.oml2owl.Oml2OwlTask, group:"oml", dependsOn: omlDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl-ventailments/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReasonVocabulary(type:io.opencaesar.owl.reason.OwlReasonTask, group:"oml", dependsOn: omlToOwlVEntailments) {
    // OWL catalog
    catalogPath = file('build/owl-ventailments/catalog.xml')
    // Input ontology IRI to reason on
    inputOntologyIri = "$dataset.rootVocabularyBundleIri".toString()
    // Entailment statements to generate and the ontologies to persist them in
    specs = [
        "$dataset.rootVocabularyBundleIri/classes = ALL_SUBCLASS".toString(),
        "$dataset.rootVocabularyBundleIri/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY".toString(),
        "$dataset.rootVocabularyBundleIri/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS".toString(),
    ]
    // Junit error report
    reportPath = file('build/reports-ventailments/reasoning.xml')
}


task omlToOwlDEntailments(type:io.opencaesar.oml2owl.Oml2OwlTask, group:"oml", dependsOn: omlDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl-dentailments/catalog.xml')
}

task owlReasonDescription(type:io.opencaesar.owl.reason.OwlReasonTask, group:"oml", dependsOn: omlToOwlDEntailments) {
    // OWL catalog
    catalogPath = file('build/owl-dentailments/catalog.xml')
    // Input ontology IRI to reason on
    inputOntologyIri = "$dataset.rootDescriptionBundleIri".toString()
    // Entailment statements to generate and the ontologies to persist them in
    specs = [
        "$dataset.rootDescriptionBundleIri/classes = ALL_SUBCLASS".toString(),
        "$dataset.rootDescriptionBundleIri/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY".toString(),
        "$dataset.rootDescriptionBundleIri/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS".toString()
    ]
    // Junit error report
    reportPath = file('build/reports-dentailments/reasoning.xml')
}

task owlReason(group:"oml", dependsOn: [owlReasonVocabulary, owlReasonDescription]) {}

/*
 * Start the headless Fuseki servers
 */

task startFuseki0(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"fuseki") {
    configurationPath = file('.fuseki0.ttl')
    outputFolderPath = file('.fuseki0')
    port = 3030
    additionalClasspathDependencies = [
    ]
    webUI = true
}

task startFuseki1(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"fuseki") {
    configurationPath = file('.fuseki1.ttl')
    outputFolderPath = file('.fuseki1')
    port = 3031
    additionalClasspathDependencies = [
    ]
    webUI = true
}

task startFuseki2(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"fuseki") {
    configurationPath = file('.fuseki2.ttl')
    outputFolderPath = file('.fuseki2')
    port = 3032
    additionalClasspathDependencies = [
        "com.github.galigator.openllet:openllet-jena:2.6.5"
    ]
    webUI = true
}

task startFuseki3(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"fuseki") {
    configurationPath = file('.fuseki3.ttl')
    outputFolderPath = file('.fuseki3')
    port = 3033
    additionalClasspathDependencies = []
    additionalJVMArguments = [
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005",
        "-Dlog4j.configurationFile=file:../log4j2.properties"
    ]
    webUI = true
}

task startFuseki4(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"fuseki") {
    configurationPath = file('.fuseki4.ttl')
    outputFolderPath = file('.fuseki4')
    port = 3034
    additionalClasspathDependencies = [
        "com.github.galigator.openllet:openllet-jena:2.6.5"
    ]
    additionalJVMArguments = [
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005"
    ]
    debug = true
    webUI = true
}

task startFuseki(group:"fuseki", dependsOn: [startFuseki0, startFuseki1, startFuseki2, startFuseki3, startFuseki4]) {}

/*
 * Stop the headless Fuseki servers
 */

task stopFuseki0(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"fuseki") {
    outputFolderPath = file('.fuseki0')
}

task stopFuseki1(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"fuseki") {
    outputFolderPath = file('.fuseki1')
}

task stopFuseki2(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"fuseki") {
    outputFolderPath = file('.fuseki2')
}

task stopFuseki3(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"fuseki") {
    outputFolderPath = file('.fuseki3')
}

task stopFuseki4(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"fuseki") {
    outputFolderPath = file('.fuseki4')
}
/*
 * Stop all running Fuseki servers.
 */
task stopFuseki(group:"fuseki") {
    if (file('.fuseki0/fuseki.pid').exists()) {
        dependsOn stopFuseki0
    }
    if (file('.fuseki1/fuseki.pid').exists()) {
        dependsOn stopFuseki1
    }
    if (file('.fuseki2/fuseki.pid').exists()) {
        dependsOn stopFuseki2
    }
    if (file('.fuseki3/fuseki.pid').exists()) {
        dependsOn stopFuseki3
    }
    if (file('.fuseki4/fuseki.pid').exists()) {
        dependsOn stopFuseki4
    }
}

startFuseki0.configure { mustRunAfter stopFuseki }
startFuseki1.configure { mustRunAfter stopFuseki }
startFuseki2.configure { mustRunAfter stopFuseki }
startFuseki3.configure { mustRunAfter stopFuseki }
startFuseki4.configure { mustRunAfter stopFuseki }

/*
 * A task to load an OWL catalog to a Fuseki dataset endpoint
 */
task owlLoad0(type:io.opencaesar.owl.load.OwlLoadTask, group:"owl", dependsOn: [owlReason]) {
    catalogPath = file('build/owl-dentailments/catalog.xml')
    endpointURL = "http://localhost:3030/tutorial".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
        "$dataset.rootDescriptionBundleIri".toString(),
        "$dataset.rootDescriptionBundleIri/classes".toString(),
        "$dataset.rootDescriptionBundleIri/properties".toString(),
        "$dataset.rootDescriptionBundleIri/individuals".toString()
    ]
}

task owlLoad1(type:io.opencaesar.owl.load.OwlLoadTask, group:"owl", dependsOn: [owlReason]) {
    catalogPath = file('build/owl-ventailments/catalog.xml')
    endpointURL = "http://localhost:3031/tutorial".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
        "$dataset.rootDescriptionBundleIri".toString(),
        "$dataset.rootVocabularyBundleIri/classes".toString(),
        "$dataset.rootVocabularyBundleIri/properties".toString(),
        "$dataset.rootVocabularyBundleIri/individuals".toString()
    ]
}

task owlLoad2(type:io.opencaesar.owl.load.OwlLoadTask, group:"owl", dependsOn: [owlReason]) {
    catalogPath = file('build/owl-ventailments/catalog.xml')
    endpointURL = "http://localhost:3032/tutorial-update".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
        "$dataset.rootDescriptionBundleIri".toString(),
        "$dataset.rootVocabularyBundleIri/classes".toString(),
        "$dataset.rootVocabularyBundleIri/properties".toString(),
        "$dataset.rootVocabularyBundleIri/individuals".toString()
    ]
}

task owlLoad3(type:io.opencaesar.owl.load.OwlLoadTask, group:"owl", dependsOn: [owlReason]) {
    catalogPath = file('build/owl-ventailments/catalog.xml')
    endpointURL = "http://localhost:3033/tutorial".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
        "$dataset.rootDescriptionBundleIri".toString(),
        "$dataset.rootVocabularyBundleIri/classes".toString(),
        "$dataset.rootVocabularyBundleIri/properties".toString(),
        "$dataset.rootVocabularyBundleIri/individuals".toString()
    ]
}

task owlLoad4(type:io.opencaesar.owl.load.OwlLoadTask, group:"owl", dependsOn: [owlReason]) {
    catalogPath = file('build/owl-ventailments/catalog.xml')
    endpointURL = "http://localhost:3034/tutorial".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
        "$dataset.rootDescriptionBundleIri".toString(),
        "$dataset.rootVocabularyBundleIri/classes".toString(),
        "$dataset.rootVocabularyBundleIri/properties".toString(),
        "$dataset.rootVocabularyBundleIri/individuals".toString()
    ]
}

task owlLoad(group:"owl", dependsOn: [owlLoad0, owlLoad1, owlLoad2, owlLoad3, owlLoad4]) {}

/*
 * A task to run a set of SPARQL queries on a Fuseki dataset endpoint
 */
task owlQuery0(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad0) {
    endpointURL = "http://localhost:3030/tutorial".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/results0')
    format = 'json'
}

task owlQuery1(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad1) {
    endpointURL = "http://localhost:3031/tutorial".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/results1')
    format = 'json'
}

task owlQuery2(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad2) {
    endpointURL = "http://localhost:3032/tutorial-query".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/results2')
    format = 'json'
}

task owlQuery3(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad3) {
    endpointURL = "http://localhost:3033/tutorial".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/results3')
    format = 'json'
}

task owlQuery4(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad4) {
    endpointURL = "http://localhost:3034/tutorial".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/results4')
    format = 'json'
}

task owlQuery(group:"owl", dependsOn: [owlQuery0, owlQuery1, owlQuery2, owlQuery3, owlQuery4]) {}

/*
 * A task to build the project, which executes several tasks together
 */
task build(group: "oml") {
    dependsOn owlReason
}

/*
 * A task to delete the build artifacts
 */
task clean(type: Delete, group: "oml") {
	delete 'build'
}

/*
 * Publish artifact to maven
 */
task omlZip(type: Zip, group:"oml") {
    from file('src/oml')
    include "**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "melaasar"
            name "Maged Elaasar"
            email "melaasar@gmail.com"
        }
    }
    scm {
        url 'https://github.com/opencaesar/'+rootProject.name
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            artifact omlZip
            pom {
                packaging = 'zip'
                withXml {
                    def root = asNode()
                    if (configurations.find { it.name == 'oml' }) {
                        def dependencies = root.appendNode('dependencies')
                        configurations.oml.resolvedConfiguration.resolvedArtifacts.each {
                            def dependency = dependencies.appendNode('dependency')
                            dependency.appendNode('groupId', it.moduleVersion.id.group)
                            dependency.appendNode('artifactId', it.moduleVersion.id.name)
                            dependency.appendNode('version', it.moduleVersion.id.version)
                            if (it.classifier != null) {
                                dependency.appendNode('classifier', it.classifier)
                                dependency.appendNode('type', it.extension)
                            }
                        }
                    }
                    root.appendNode('name', project.ext.title)
                    root.appendNode('description', project.description)
                    root.appendNode('url', 'https://github.com/opencaesar/'+rootProject.name)
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

tasks.named('wrapper') {
   gradleVersion = '6.5.1' //version required
}

/*
 * Integration with the Eclipse IDE
 */ 
apply plugin: 'eclipse'

eclipse {
    synchronizationTasks omlDependencies
}
